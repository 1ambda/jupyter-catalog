buildscript {
    ext {
        springBootVersion = '2.1.5.RELEASE'
        gradleAptVersion = '0.19'
        owaspVersion = '3.3.1'
        queryDslPluginVersion = '1.0.10'
        sonarQubeVersion = '2.6.2'
        propdepsVersion = '0.0.9.RELEASE'
        shadowVersion = '4.0.2'
        gradleTestLoggerVersion = '1.6.0'
        gradleScalaTestVersion = '0.19'
        gradleVersioningPluginVersion = '2.8.2'
        lombokPluginVersion = '3.1.0'
        jibPluginVersion = '1.3.0'
        kotlinPluginVersion = '1.3.30'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.spring.io/plugins-release' }
        maven {
            name "typesafe-maven-release"
            url "https://repo.typesafe.com/typesafe/maven-releases"
        }
        maven {
            name "Spark Packages Repo"
            url "http://dl.bintray.com/spark-packages/maven"
        }
        ivy {
            name "typesafe-ivy-release"
            url "https://repo.typesafe.com/typesafe/ivy-releases"
            layout "ivy"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("net.ltgt.gradle:gradle-apt-plugin:${gradleAptVersion}")
        classpath("org.owasp:dependency-check-gradle:${owaspVersion}")
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarQubeVersion}"
        classpath "io.spring.gradle:propdeps-plugin:${propdepsVersion}"
        classpath "com.github.jengelman.gradle.plugins:shadow:${shadowVersion}"
        classpath "com.adarshr:gradle-test-logger-plugin:${gradleTestLoggerVersion}"
        classpath "gradle.plugin.net.nemerosa:versioning:${gradleVersioningPluginVersion}"
        classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${queryDslPluginVersion}"
        classpath("io.franzbecker:gradle-lombok:${lombokPluginVersion}")
        classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:${jibPluginVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinPluginVersion}"
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'com.adarshr.test-logger'

    repositories {
        mavenCentral()
        maven { url "http://jcenter.bintray.com" }
    }

    idea {
        module {
            sourceDirs += file('out/production/classes/generated/')
            // generatedSourceDirs += file('out/production/classes/generated/')
        }
    }

    testlogger {
        theme 'standard'
        showExceptions true
        slowThreshold 10000
        showSummary true
        showPassed true
        showSkipped true
        showFailed true
        showStandardStreams false
        showPassedStandardStreams true
        showSkippedStandardStreams true
        showFailedStandardStreams true
    }


    ext {
        // :module-api-swagger
        swaggerAnnotationVersion = '1.5.21'
        jacksonJsr310Version = '2.9.7'

        feignVersion = '9.4.0'
        feignFormVersion = '2.1.0'
        oltuVersion = '1.0.1'
        migbase64Version = '2.2'

        // common
        javaxValidationVersion = '2.0.1.Final'
        guavaVersion = '26.0-jre'
        springBootAdminVersion = '2.0.4'
        micrometerVersion = '1.0.6'
        springfoxVersion = '2.9.2'
        logbookVersion = '1.13.1'
        jjwtVersion = '0.9.1'

        // aws
        awsSdkDynamoVersion = '1.11.547'

        // storage, repository
        prestoVersion = '0.206'
        mysqlConnectorVersion = '8.0.13'
        springKafkaVersion = '2.2.0.RELEASE'
        springDataLdapVersion = '2.1.6.RELEASE'

        // test
        testcontainersVersion = '1.10.1'

        // code generation related
        lombokVersion = "1.18.8"
        queryDslVersion = '4.2.1'
        hibernateModelGenVersion = '5.4.1.Final'

        // kotlin
        kotlinVersion = '1.3.30'

        // scala, spark, flink: compile
        scalaVersion = '2.11'
        scalaVersionRevision = '12'

        sparkVersion = '2.4.0'
        sparkJacksonVersion = '2.6.5'
        sparkDariaVersion = '0.25.0'
        awsHadoopVersion = '2.8.5'
        semverVresion = '2.2.0'

        flinkVersion = "1.7.1"
        flinkJacksonVersion = '2.9.8'

        typesafeConfigVersion = '1.3.3'
        shapelessVersion = "2.3.3"
        pureconfigVersion = "0.10.1"
        json4sVersion = '3.6.5'

        // scala, spark: test
        junitVersion = '5.4.2'
        scalaTestVersion = '3.0.5'
        sparkTestingBaseVersion = '0.11.0'
        // https://mvnrepository.com/artifact/com.holdenkarau/spark-testing-base
        sparkFastTestVersion = '0.15.0'
    }
}

subprojects {
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'org.sonarqube'

    project.group = 'io.github.jupyter.catalog'

    targetCompatibility = 1.8
    sourceCompatibility = 1.8
    [compileJava, compileTestJava]*.options.collect {
        options -> options.encoding = 'UTF-8'
    }

    if (project.name == 'module-core-jdbc' || project.name == 'service-catalog-api') {

        apply plugin: 'io.spring.dependency-management'
        apply plugin: 'io.franzbecker.gradle-lombok'

        dependencies {
            // common
            compile("com.google.guava:guava:${guavaVersion}")
            compile("javax.validation:validation-api:${javaxValidationVersion}")

            // spring
            compile 'org.springframework.boot:spring-boot-starter'
            compile('org.springframework.boot:spring-boot-starter-actuator')
            runtime('org.springframework.boot:spring-boot-devtools')

            // code generation
            compileOnly("org.projectlombok:lombok:${lombokVersion}")
            testCompile("org.projectlombok:lombok:${lombokVersion}")

            // test
            testCompile 'org.springframework.boot:spring-boot-starter-test'
            testCompile("org.testcontainers:testcontainers:${testcontainersVersion}")
            testCompile("org.testcontainers:kafka:${testcontainersVersion}")
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            }
        }
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '5.4.1'
    }

    sonarqube {
        properties {
            if (project.hasProperty('groupName')) {
                property 'sonar.projectKey', project.property('groupName') + ':' + project.name
            }
        }
    }
}